#
# this simulates OWLIM's owl-horst_partialRDFS_builtin.pie rule file that builds
# on the rule semantics for OWL as published by ter Horst which is based on Hayes'
# work for axiomatizing RDF Schema;
# I have split up rule $owl_FunctProp into $owl_FunctPropObject and
# $owl_FunctPropDatatype, since the nature of the object of the triple either
# leads to a owl:sameAs statement or to the bottom type owl:Nothing
#
# @author Hans-Ulrich Krieger
# @version Tue Jan 10 15:53:00 CET 2012
#


# NOTE: $rdf1_rdfs4a_4b (= rdf1 & rdfs4a & rdfs4b) modified;
# not interested that ?s, ?p, and ?o are all of rdf:type rdfs:Resource;
# only add that ?p is of rdf:type rdf:Property
$rdf1_rdfs4a_4b
?t ?_s ?p ?_o ?i1 ?i2
->
?t ?p <rdf:type> <rdf:Property>  ?i1 ?i2


$rdfs2
?t ?s ?p ?_o ?i1 ?i2
?t ?p <rdfs:domain> ?dom ?i1 ?i2
->
?t ?s <rdf:type> ?dom ?i1 ?i2
@test
?p != <rdf:type>


# added second clause (HUK)
$rdfs3
?t ?_s ?p ?o ?i1 ?i2
?t ?p <rdf:type> <owl:ObjectProperty> ?i1 ?i2
?t ?p <rdfs:range> ?rng ?i1 ?i2
->
?t ?o <rdf:type> ?rng ?i1 ?i2


# NOTE: redundant rule -- implemented through owl:TransitiveProperty
#$rdfs5
#?p <rdfs:subPropertyOf> ?q
#?q <rdfs:subPropertyOf> ?r
#->
#?p <rdfs:subPropertyOf> ?r
#@test
#?p != ?q
#?p != ?r
#?q != ?r


$rdfs6
?t ?p <rdf:type> <rdf:Property> ?i1 ?i2
->
?t ?p <rdfs:subPropertyOf> ?p ?i1 ?i2


$rdfs7
?t ?s ?p ?o ?i1 ?i2
?t ?p <rdfs:subPropertyOf> ?q ?i1 ?i2
->
?t ?s ?q ?o ?i1 ?i2
@test
?p != ?q


# replaced rdfs:Class by owl:Class;
# or we add a further axiom that owl:Class is a subclass of rdfs:Class as it is
# enforced for OWL Lite and OWL DL
# NOTE: this rule only implements rdfs10 (note that ?c is a subclass of rdfs:Resource)
$rdfs8_10
?t ?c <rdf:type> <owl:Class> ?i1 ?i2
->
?t ?c <rdfs:subClassOf> ?c ?i1 ?i2


# universal instantiation
$rdfs9
?t ?i <rdf:type> ?c ?i1 ?i2
?t ?c <rdfs:subClassOf> ?d ?i1 ?i2
->
?t ?i <rdf:type> ?d ?i1 ?i2
@test
?c != ?d


# NOTE: redundant rule -- implemented through owl:TransitiveProperty
# NOTE: no rule defined for rdfs:subPropertyOf -- again redundant through
#       the characterization as being a transitive property
#$rdfs11
#?c <rdfs:subClassOf> ?d ?i1 ?i2
#?d <rdfs:subClassOf> ?e ?i1 ?i2
#->
#?c <rdfs:subClassOf> ?e ?i1 ?i2
#@test
#?c != ?d
#?c != ?e
#?d != ?e


# NOTE: $rdfs12 omitted -- deals with rdfs:member
# will not fire for our OWL ontologies


# NOTE: $rdfs13 omitted -- deals with rdfs:Literal
# will not fire for our OWL ontologies


$owl_invOf
?t ?s ?p ?o ?i1 ?i2
?t ?p <owl:inverseOf> ?q ?i1 ?i2
->
?t ?o ?q ?s ?i1 ?i2


$owl_invOfBySymProp
?t ?p <rdf:type> <owl:SymmetricProperty> ?i1 ?i2
->
?t ?p <owl:inverseOf> ?p ?i1 ?i2


$owl_SymPropByInverse
?t ?p <owl:inverseOf> ?p ?i1 ?i2
->
?t ?p <rdf:type> <owl:SymmetricProperty> ?i1 ?i2


# original rule name is: rdfp3;
# rule was _not_ part of the OWLIM definition (why?)
$owl_SymmetricProperty
?t ?p <rdf:type> <owl:SymmetricProperty> ?i1 ?i2
?t ?s ?p ?o ?i1 ?i2
->
?t ?o ?p ?s ?i1 ?i2


$owl_TransProp
?t ?p <rdf:type> <owl:TransitiveProperty> ?i1 ?i2
?t ?x ?p ?y ?i1 ?i2
?t ?y ?p ?z ?i1 ?i2
->
?t ?x ?p ?z ?i1 ?i2
@test
?x != ?y
?y != ?z


# second clause has been added to guarantee proper application context
$owl_FunctPropObject
?t ?p <rdf:type> <owl:FunctionalProperty> ?i1 ?i2
?t ?p <rdf:type> <owl:ObjectProperty> ?i1 ?i2
?t ?x ?p ?y ?i1 ?i2
?t ?x ?p ?z ?i1 ?i2
->
?t ?y <owl:sameAs> ?z ?i1 ?i2
@test
?y != ?z
?p != <rdf:type>


# NOT part of the original rule set, a kind of integrity constraint, deriving
# that an individual is of type owl:Nothing;
# NOTE:  currently commented out  !!!!!!!!!!!!!!
#$owl_FunctPropDatatype
#?p <rdf:type> <owl:FunctionalProperty>
#?p <rdf:type> <owl:DatatypeProperty>
#?x ?p ?y
#?x ?p ?z
#->
#?x <rdf:type> <owl:Nothing>
#@test
#?y != ?z
#?p != <rdf:type>


# holds for both datatype and object properties
$owl_InvFunctProp
?t ?p <rdf:type> <owl:InverseFunctionalProperty> ?i1 ?i2
?t ?y ?p ?x ?i1 ?i2
?t ?z ?p ?x ?i1 ?i2
->
?t ?y <owl:sameAs> ?z ?i1 ?i2
@test
?y != ?z
?p != <rdf:type>


$owl_sameAsCopySubj
?t ?x <owl:sameAs> ?y ?i1 ?i2
?t ?x ?p ?z ?i1 ?i2
->
?t ?y ?p ?z ?i1 ?i2
@test
?x != ?y


# called owl_sameAsCopyPred in the original file, but owl:sameAs can NOT
# be applied to predicates, so replace sameAs by owl:equivalentProperty
# NOTE: original rule was never applied -- there are no properties p and q
#       that are connected by owl:sameAs
$owl_equivalentPropertyCopyPred
?t ?p <owl:equivalentProperty> ?q ?i1 ?i2
?t ?x ?p ?y ?i1 ?i2
->
?t ?x ?q ?y ?i1 ?i2
@test
?p != ?q


$owl_sameAsCopyObj
?t ?x <owl:sameAs> ?y ?i1 ?i2
?t ?z ?p ?x ?i1 ?i2
->
?t ?z ?p ?y ?i1 ?i2
@test
?x != ?y


$owl_EquivClassBySubClass
?t ?x <rdfs:subClassOf> ?y ?i1 ?i2
?t ?y <rdfs:subClassOf> ?x ?i1 ?i2
->
?t ?x <owl:equivalentClass> ?y ?i1 ?i2
@test
?x != ?y


$owl_EquivPropBySubProp
?t ?p <rdfs:subPropertyOf> ?q ?i1 ?i2
?t ?q <rdfs:subPropertyOf> ?p ?i1 ?i2
->
?t ?p <owl:equivalentProperty> ?q ?i1 ?i2
@test
?p != ?q


$owl_typeByAllVal
?t ?x <owl:onProperty> ?p ?i1 ?i2
?t ?u <rdf:type> ?x ?i1 ?i2
?t ?x <owl:allValuesFrom> ?y ?i1 ?i2
?t ?u ?p ?v ?i1 ?i2
->
?t ?v <rdf:type> ?y ?i1 ?i2


$owl_typeByHasVal
?t ?r <owl:onProperty> ?p ?i1 ?i2
?t ?r <owl:hasValue> ?v ?i1 ?i2
?t ?i ?p ?v ?i1 ?i2
->
?t ?i <rdf:type> ?r ?i1 ?i2


$owl_AttrByHasVal
?t ?x <owl:hasValue> ?y ?i1 ?i2
?t ?x <owl:onProperty> ?p ?i1 ?i2
?t ?u <rdf:type> ?x ?i1 ?i2
->
?t ?u ?p ?y ?i1 ?i2


$owl_typeBySomeVal
?t ?q <rdf:type> ?c ?i1 ?i2
?t ?r <owl:onProperty> ?p ?i1 ?i2
?t ?r <owl:someValuesFrom> ?c ?i1 ?i2
?t ?i ?p ?q ?i1 ?i2
->
?t ?i <rdf:type> ?r ?i1 ?i2


$owl_subClassByInters1
?t ?c <owl:intersectionOf> ?x ?i1 ?i2
?t ?x <rdf:first> ?y ?i1 ?i2
->
?t ?c <rdfs:subClassOf> ?x ?i1 ?i2
?t ?c <rdfs:subClassOf> ?y ?i1 ?i2


$owl_subClassByInters2
?t ?x <rdf:rest> ?y ?i1 ?i2
?t ?c <owl:intersectionOf> ?x ?i1 ?i2
->
?t ?x <owl:intersectionOf> ?y ?i1 ?i2
@test
?y != <rdf:nil>


$owl_typeByIntersect_1
?t ?b <rdf:first> ?c ?i1 ?i2
?t ?b <rdf:rest> <rdf:nil> ?i1 ?i2
?t ?z <owl:intersectionOf> ?b ?i1 ?i2
?t ?i <rdf:type> ?c ?i1 ?i2
->
?t ?i <rdf:type> ?b ?i1 ?i2


$owl_typeByIntersect_2
?t ?b <rdf:first> ?c ?i1 ?i2
?t ?i <rdf:type> ?c ?i1 ?i2
?t ?i <rdf:type> ?b ?i1 ?i2
?t ?n <owl:intersectionOf> ?b ?i1 ?i2
->
?t ?i <rdf:type> ?n ?i1 ?i2
